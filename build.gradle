plugins {
  id 'org.springframework.boot' version '2.5.6'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'java'
  id 'war'
  // Domaコンパイルプラグインの使用を定義
  id 'com.diffplug.eclipse.apt' version '3.33.1'
  id 'org.seasar.doma.compile' version '1.1.0'
}

// 注釈処理の記述
compileJava {
  aptOptions {
    processorArgs = [
      'doma.dao.subpackage' : 'impl', 'doma.dao.suffix' : 'Impl'
    ]
  }
}

group = 'com.inusuffron'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
  // 依存関係の定義
  domaGenRuntime
}

repositories {
  mavenCentral()
  // 次の1行を追加
  maven {url 'https://oss.sonatype.org/content/repositories/snapshots/'}
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  compileOnly 'org.projectlombok:lombok'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  runtimeOnly 'org.postgresql:postgresql'
  annotationProcessor 'org.projectlombok:lombok'
  providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  // 以下、DOMAの追加
  implementation 'org.seasar.doma:doma-core:2.50.0'
    implementation 'org.seasar.doma.boot:doma-spring-boot-starter:1.5.0'
    annotationProcessor 'org.seasar.doma:doma-processor:2.50.0'
  // doma-gen設定
  domaGenRuntime 'org.seasar.doma:doma-gen:2.28.0'
  // JDBCでpostgreSQLに接続する設定
  domaGenRuntime 'org.postgresql:postgresql:42.3.1'
  // javax.validationを依存関係に追加
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  // WebjarsによりBootstrapを依存関係に追加(Bootstrap5よりjQueryの記述は不要)
  implementation 'org.webjars:bootstrap:5.1.3'
  // 認証にspring securityを使うための設定
  implementation 'org.springframework.boot:spring-boot-starter-security'
  // thymeleafでspring securityを使うための設定
  implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
  // Java8以降のLocalDate, LocalTime, LocalDateTimeをThymeleafのユーティリティオブジェクトで使用するための設定
  implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time'
}

// doma-genタスクの設定
// 注: doma-genタスクでエンティティの自動生成を行ったら、DBのシーケンスを利用した場合、
// エンティティクラスの@idが付加されたカラムに付いて以下の修正を施さないと実行時エラーとなる。
// （DOMA2のエンティティクラスのドキュメントを参照)
// 修正前: @GeneratedValue(strategy = GenerationType.IDENTITY)
// 修正後: @GeneratedValue(strategy = GenerationType.SEQUENCE)
//        @SequenceGenerator(sequence = "テーブル生成時に自動生成されたシーケンス名")
task gen {
  group = 'doma-gen'
  doLast {
    ant.taskdef(resource: 'domagentask.properties',
        classpath: configurations.domaGenRuntime.asPath)
    ant.gen(url: 'jdbc:postgresql://localhost:5432/cinecom', user: 'isaku', password: '4,5&6JackieMcLean', schemaName: 'isaku') {
      entityConfig(destDir: 'src/main/java', packageName: 'com.inusufforn.cinecom.entity')
      daoConfig(destDir: 'src/main/java', packageName: 'com.inusufforn.cinecom.dao')
      sqlConfig(destDir: 'src/main/resources')
    }
  }
}

// 元からあったものをコメント化
//test {
//	useJUnitPlatform()
//}
